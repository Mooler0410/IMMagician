<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1">
    <!-- The above 3 meta tags *must* come first in the head; any other head content must come *after* these tags -->
    <meta name="description" content="">
    <meta name="author" content="">

    <title>Mock3d-Template</title>

    <!-- Google font -->
    <link href='https://fonts.googleapis.com/css?family=Open+Sans:400,300' rel='stylesheet' type='text/css'>

    <!-- Bootstrap core CSS -->
    <link href="css/bootstrap.css" rel="stylesheet"> <!--have bootstrap.min.css-->
    <link href="css/offcanvas.css" rel="stylesheet">
    <link href="css/bootstrap-slider.css" rel="stylesheet">
    <link href="css/jquery.minicolors.css" rel="stylesheet">


    <!-- Custom styles for this template -->
    <link href="css/mock3d.css" rel="stylesheet">

    <script id="vertex-shader" type="x-shader/x-vertex">
        attribute vec4 vPosition;

        // deprecated
        uniform float lightNum;
        uniform vec3 lightColor[1];
        uniform float lightX, lightY, lightZ;
        uniform float Dir_lightX, Dir_lightY, Dir_lightZ;
        uniform float coordX, coordY, coordZ;

        varying vec4 v_position;
        varying vec3 defaultLightColor;

        varying vec3 p_light;
        varying vec3 dir_light;
        varying vec3 p_eye;

        void main() {
            gl_Position = vPosition;
            defaultLightColor = lightColor[0];
            p_light = vec3(lightX, lightY, lightZ);
            dir_light = vec3(Dir_lightX, Dir_lightY, Dir_lightZ);
            p_eye = vec3(coordX, coordY, coordZ);
            v_position = vPosition;
        }
    </script>

    <script id="fragment-shader" type="x-shader/x-fragment">
        #define MAX_TH 10000000.0
        #define PI 3.14159265359

        precision mediump float;

        varying vec4 v_position;
        varying vec3 defaultLightColor;
        varying vec3 p_light;
        varying vec3 dir_light;
        varying vec3 p_eye;


        uniform sampler2D uSamplerTexture;
        uniform sampler2D defaultTexture;

        uniform int currentLight;

        uniform bool lightsOnly;

        uniform bool checkAreaLight;
        uniform bool checkDirectionalLight;

        uniform float styleBright, styleDark;

        uniform float specSharpness, specBlurriness;

        uniform float lightIntensity;

        float a02, a12, a22, a21, a00, s0, s1, s2;
        bool insides[10];
        float etas[10];
        vec3 pc[10];
        vec3 v2, vup;
        float shadow_intensity;

        const int N = 5;
        const int M = 7;

        vec4 defaultColor = vec4(0.5, 0.5, 0.5, 1.0);

        float constrain(float ip, float ip_start, float ip_end, float op_start, float op_end) {
            float slope = (op_end - op_start) / (ip_end - ip_start);
            return op_start + slope * (ip - ip_start);
       }

       bool isTransparent(int type){
            if (type == 1)
                return true;
            else
                return false;
       }

        bool isReflective(int type){
            if (type == 0)
                return true;
            else
                return false;
        }

        vec3 random(vec3 p) {
            p = vec3(dot(p, vec3(127.1, 311.7, 74.7)),
            dot(p, vec3(269.5, 183.3, 246.1)),
            dot(p, vec3(113.5, 271.9, 124.6)));

            return fract(sin(p) * 43758.5453123);
        }

        vec4 diffuse(vec3 n, vec3 v_light, vec4 color) {
            float cos = (dot(n, v_light) + 1.0) / 2.0;

            float tmin = min(styleBright, styleDark);
            float tmax = max(styleBright, styleDark);

            float t = (cos - tmin) / (tmax - tmin);

            if (t > 1.0)
                t = 1.0;
            if (t < 0.0)
                t = 0.0;

            return t * color;
        }

        vec4 spec(vec3 n, vec3 npe, vec3 v_light, vec4 color) {
            vec3 RVector = -v_light + 2.0 * dot(n, v_light) * n;
            float s = dot(RVector, -npe);

            float smin = min(specBlurriness, specSharpness);
            float smax = max(specBlurriness, specSharpness);

            s = (s - smin) / (smax - smin);

            if (s > 1.0)
                s = 1.0;
            else if (s < 0.0)
                s = 0.0;
            return s * color;
        }

        vec4 general_quadrics(int type,
            vec3 v2, vec3 vup, vec3 pc,
            vec3 pe, vec3 npe, bool inside, out float th, out vec3 fp) {

            th = MAX_TH;

            // find roots
            vec3 n0 = normalize(cross(vup, v2));
            vec3 n1 = normalize(cross(n0, v2));
            vec3 n2 = normalize(v2);

            float A = a02 * dot(n0, npe) * dot(n0, npe) / (s0 * s0) + a12 * dot(n1, npe) * dot(n1, npe) / (s1 * s1) +
                      a22 * dot(n2, npe) * dot(n2, npe) / (s2 * s2);
            float B = 2.0 * a02 * dot(n0, npe) * dot(n0, pe - pc) / (s0 * s0) +
                      2.0 * a12 * dot(n1, npe) * dot(n1, pe - pc) / (s1 * s1) +
                      2.0 * a22 * dot(n2, npe) * dot(n2, pe - pc) / (s2 * s2) + a21 * dot(n2, npe) / s2;
            float C = a02 * dot(n0, pe - pc) * dot(n0, pe - pc) / (s0 * s0) +
                      a12 * dot(n1, pe - pc) * dot(n1, pe - pc) / (s1 * s1) +
                      a22 * dot(n2, pe - pc) * dot(n2, pe - pc) / (s2 * s2) + a21 * dot(n2, pe - pc) / s2 + a00;


            float delta = B * B - 4.0 * A * C;

            if ((!inside && delta >= 0.0 && -B - sqrt(delta) >= 0.0) || (inside && delta >= 0.0 && -B + sqrt(delta) >= 0.0)) {

                if (a21 > 0.0)
                    th = -C / B;
                else if (inside == true)
                    th = (-B + sqrt(delta)) / (2.0 * A);
                else
                    th = (-B - sqrt(delta)) / (2.0 * A);
                vec3 ph = pe + th * npe;
                fp = 2.0 * a02 * n0 * dot(n0, ph - pc) / (s0 * s0) + 2.0 * a12 * n1 * dot(n1, ph - pc) / (s1 * s1) +
                     2.0 * a22 * n2 * dot(n2, ph - pc) / (s2 * s2) + a21 * n2 / s2;
                fp = normalize(fp);

                vec4 c;
                if (type == 0 || type == 1) {
                    float x = dot(n0, ph - pc) / s0;
                    float y = dot(n1, ph - pc) / s1;
                    float z = dot(n2, ph - pc) / s2;
                    float v = acos(z) / PI;
                    float u = acos(y / sin(PI * v)) / (2.0 * PI);
                    c = texture2D(uSamplerTexture, vec2(u, v));
                } else {
                    float x = dot(n0, ph - pc);
                    float y = dot(n1, ph - pc);
                    float u = x - float(floor(x));
                    if (x < 0.0)
                        u = 1.0 - u;
                    float v = y - float(floor(y));
                    if (y < 0.0)
                        v = 1.0 - v;
                    if (type == 5)
                        c = texture2D(uSamplerTexture, vec2(u, v));
                    else
                        c = texture2D(defaultTexture, vec2(u, v));
                }

                vec3 v_light;
                if (checkAreaLight) {
                    vec4 colors = vec4(0., 0., 0., 0.);

                    for (int i = 0; i < N; i++) {
                        for (int j = 0; j < M; j++) {
                            float i_float = float(i);
                            float j_float = float(j);
                            vec3 p_light_tmp = p_light + i_float * n0 + j_float * n1 + random(p_light + i_float * n0 + j_float * n1);
                            v_light = normalize(p_light_tmp - ph);
                            colors += diffuse(fp, v_light, vec4(defaultLightColor * lightIntensity, 1.) + c) +
                                      spec(fp, npe, v_light, vec4(defaultLightColor * lightIntensity, 1.) + c);
                        }
                    }
                    colors /= float(N * M);
                    return colors;
                }
                else {

                    if (checkDirectionalLight)
                        v_light = normalize(-dir_light);
                    else
                        v_light = normalize(p_light - ph);

                    return diffuse(fp, v_light, vec4(defaultLightColor * lightIntensity, 1.) + c) +
                           spec(fp, npe, v_light, vec4(defaultLightColor * lightIntensity, 1.) + c);;
                }

            } else {
                return defaultColor;
            }
        }

        void set_a(int type) {
            if (type == 0)//ellipsoid
            {
                a02 = 1., a12 = 1., a22 = 1., a21 = 0., a00 = -1.;
                s0 = 0.35;
                s1 = 0.35;
                s2 = 0.35;
                v2 = vec3(0., 0., -1.);
                vup = vec3(0., 1., 0.);
            }
            else if (type == 1)//sphere
            {
                a02 = 1., a12 = 1., a22 = 1., a21 = 0., a00 = -1.;
                s0 = 0.35;
                s1 = 0.35;
                s2 = 0.35;
                v2 = vec3(0., 0., -1.);
                vup = vec3(0., 1., 0.);
            }
            else if (type == 2)//hyperboloid of one sheet
            {
                a02 = 1., a12 = -1.0, a22 = 1., a21 = 0., a00 = -1.;
                s0 = 0.01;
                s1 = 0.03;
                s2 = 0.01;
                v2 = vec3(0., 0., -1.);
                vup = vec3(0., 1., 0.);
            }
            else if (type == 3)//hyperboloid of 2 sheets
            {
                a02 = 1., a12 = -1.0, a22 = 1., a21 = 0., a00 = 1.;
                s0 = 0.01;
                s1 = 0.03;
                s2 = 0.01;
                v2 = vec3(0., 0., -1.);
                vup = vec3(0., 1., 0.);
            }
            else if (type == 4)//cylinder
            {
                a02 = 1., a12 = 0., a22 = 1., a21 = 0., a00 = -1.;
                s0 = 0.03;
                s1 = 0.03;
                s2 = 0.03;
                v2 = vec3(0., 0., -1.);
                vup = vec3(0., 1., 0.);
            }
            else if (type == 5)//infinite sphere
            {
                a02 = 1., a12 = 1., a22 = 1., a21 = 0., a00 = -1.;
                s0 = 5.0;
                s1 = 5.0;
                s2 = 5.0;
                v2 = vec3(0., 0., -1.);
                vup = vec3(0., 1., 0.);

            }
            else if (type == 6)//plane
            {
                a02 = 0., a12 = 0., a22 = 0., a21 = 1., a00 = 0.;
                s0 = 1.0;
                s1 = 1.0;
                s2 = 1.0;
                v2 = vec3(0., 1., 0.);
                vup = vec3(1., 0., 0.);
            }
        }

        bool in_shadow(vec3 p, int obj_intersected) {
            if (checkAreaLight == false) {
                //check with each object
                float count = 0.;
                float th_tmp;
                vec3 fp_tmp;
                for (int i = 0; i < 5; i++) {
                    set_a(i);   //we set the parameters
                    //we try to find an intersection of the ray
                    if (isTransparent(i))
                        continue;
                    vec3 pe_s = p;                      //eye position/origin of that ray becomes the pixel itself
                    vec3 p_s = p_light;                //end point of the ray will be the light position
                    vec3 npe_s; // = normalize(p_s - pe_s);   //ray direction npe_s;

                    if (checkDirectionalLight)
                    npe_s =  normalize(-dir_light);
                    else
                    npe_s = normalize(p_s - pe_s);
                    vec3 v2_s = vec3(0, 0, -1);            //viewing direction will be reversed in this case
                    vec3 vup_s = -1. * vup;                    //vup stays the same

                    vec4 clr = general_quadrics(i, v2_s, vup_s, pc[i], pe_s, npe_s, insides[i], th_tmp, fp_tmp);

                    if (th_tmp != MAX_TH && th_tmp > 0. && obj_intersected != 5 && i != obj_intersected)  //for object to cast shadow, it has to lie between t=0 and t=1 and it cant cast shadow on its own
                    count += 1.;
                }

                if (count != 0.) {
                    shadow_intensity = 0.5 - count * 0.1;
                    return true;
                }

                return false;
            } else {
                vec3 n0 = vec3(1., 0., 1.);
                vec3 n1 = vec3(0., 1., 1.);
                float S = 0.;

                const int A = 7;

                vec3 fp_tmp;
                for (int m = 0; m < A; m++) {
                    for (int n = 0; n < A; n++) {
                        float m_float = (float(m) + random(p).x);
                        float n_float = (float(n) + random(p).y);
                        vec3 p_s = p_light;
                        p_s.x +=  m_float;
                        p_s.y +=  n_float;

                        //check with each object
                        float th_min_s = MAX_TH, th_s[10];
                        for (int i = 0; i < 5; i++) {
                            set_a(i);   //we set the parameters
                            //we try to find an intersection of the ray
                            if (isTransparent(i))
                                continue;
                            vec3 pe_s = p;                      //eye position/origin of that ray becomes the pixel itself
                            vec3 npe_s = normalize(p_s - pe_s);   //ray direction npe_s;
                            vec3 v2_s = vec3(0, 0, -1);            //viewing direction will be reversed in this case
                            vec3 vup_s = -1. * vup;                    //vup stays the same

                            vec4 clr = general_quadrics(i, v2_s, vup_s, pc[i], pe_s, npe_s, insides[i], th_s[i], fp_tmp);

                            if (th_s[i] < th_min_s && th_min_s > 0. && obj_intersected != 5 && i != obj_intersected)  //for object to cast shadow, it has to lie between t=0 and t=1 and it cant cast shadow on its own
                            { th_min_s = th_s[i]; }
                        }

                        if (th_min_s != MAX_TH)
                        S = S + 1.;
                    }
                }
                float A2 = float(A * A);

                if (S > 0.) {
                    S = constrain(S, 0.0, A2, 1.0, 0.30);

                    shadow_intensity = S;
                    return true;
                }
                return false;
            }
        }



        void main() {
            vec3 p = vec3(v_position[0], v_position[1], v_position[2]);
            vec3 pe = p_eye;

            // quadrics parameters --------------------------------------------------------------
            vec3 npe = normalize(p - pe);

            pc[0] = vec3(0.35, -0.1, -1.2);         //ellipsoid
            pc[1] = vec3(-0.5, 0.0, -0.5);           //sphere
            pc[2] = vec3(-0.10, 0.0, -0.95);          //hyperboloid of 1 sheet
            pc[3] = vec3(-0.80, 0.0, -1.2);         //hyperboloid of 2 sheets
            pc[4] = vec3(0.5, 0.0, -0.2);           //cylinder
            pc[5] = vec3(0.0, 0.0, 0.0);            //infinite sphere
            pc[6] = vec3(0.0, -0.8, 0.0);           //plane

            insides[0] = false;
            insides[1] = false;
            insides[2] = false;
            insides[3] = false;
            insides[4] = false;
            insides[5] = true;
            insides[6] = false;

            etas[0] = 1.5;
            etas[1] = 1.0;
            etas[2] = 1.5;
            etas[3] = 1.0;
            etas[4] = 1.0;
            etas[5] = 1.0;
            etas[6] = 1.0;

            int obj_intersected = -1;
            float th_min = MAX_TH;
            vec3 fp_min;
            vec3 pc_min;
            float eta_min;
            vec4 color_min = vec4(0., 0., 0., 0.);

            // first rendering opaque
            for(int i = 0; i < 7; i++) {
                set_a(i);
                float t;
                vec3 fp;
                vec4 clr = general_quadrics(i, v2, vup, pc[i], pe, npe, insides[i], t, fp);
                if (t < th_min) {
                    th_min = t;
                    fp_min = fp;
                    pc_min = pc[i];
                    eta_min = etas[i];
                    color_min = clr;
                    obj_intersected = i;
                }
            }

            if (th_min == MAX_TH) {
                gl_FragColor = defaultColor;
                return;
            } else {
                if (isReflective(obj_intersected) || isTransparent(obj_intersected)) {
                    // enter one transparent object
                    bool enter = false;

                    for (int rayDepth = 0; rayDepth < 3; rayDepth++) {
                        float cosT = dot(npe, fp_min);
                        pe = pe + th_min * npe;


                        if (isReflective(obj_intersected)) {
                            npe = normalize(npe - 2. * cosT * fp_min);
                            th_min = MAX_TH;
                            for(int i = 0; i < 7; i++) {
                                set_a(i);
                                float t;
                                vec3 fp;
                                vec4 clr = general_quadrics(i, v2, vup, pc[i], pe, npe, insides[i], t, fp);
                                if (t < th_min) {
                                    th_min = t;
                                    fp_min = fp;
                                    eta_min = etas[i];
                                    pc_min = pc[i];
                                    color_min = clr;
                                    obj_intersected = i;
                                }
                            }
                            if (th_min == MAX_TH) {
                                gl_FragColor = color_min;
                                return;
                            }

                        } else if(isTransparent(obj_intersected)) {
                            float delta = (cosT * cosT - 1.0) / (eta_min * eta_min) + 1.0;
                            // refraction
                            if (delta >= 0.00001) {
                                npe = normalize(npe / eta_min + (cosT / eta_min + sqrt(delta)) * fp_min);
                                if (enter == false) {
                                    enter = true;
                                } else {
                                    enter = false;
                                }
                                if (enter) {
                                    eta_min = 1.0 / eta_min;

                                    float t;
                                    vec3 fp;
                                    set_a(obj_intersected);
                                    vec4 clr = general_quadrics(obj_intersected, v2, vup, pc_min, pe, npe, true, t, fp);
                                    th_min = t;
                                    fp_min = fp;
                                } else {
                                    th_min = MAX_TH;
                                    for(int i = 0; i < 7; i++) {
                                        set_a(i);
                                        float t;
                                        vec3 fp;
                                        vec4 clr = general_quadrics(i, v2, vup, pc[i], pe, npe, insides[i], t, fp);
                                        if (t < th_min) {
                                            th_min = t;
                                            fp_min = fp;
                                            eta_min = etas[i];
                                            pc_min = pc[i];
                                            color_min = clr;
                                            obj_intersected = i;
                                        }
                                    }
                                    if (th_min == MAX_TH) {
                                        gl_FragColor = color_min;
                                        return;
                                    }
                                }
                                // special case: nothing happen
                            } else if (delta < 0.00001 && delta > -0.00001) {
                                enter = false;
                                th_min = MAX_TH;
                                int intersected = obj_intersected;
                                for(int i = 0; i < 7; i++) {

                                    set_a(i);
                                    if (i == intersected)
                                        continue;
                                    float t;
                                    vec3 fp;
                                    vec4 clr = general_quadrics(i, v2, vup, pc[i], pe, npe, insides[i], t, fp);
                                    // assume we don't get transparent object anymore
                                    if (t < th_min) {
                                        th_min = t;
                                        fp_min = fp;
                                        eta_min = etas[i];
                                        pc_min = pc[i];
                                        color_min = clr;
                                        obj_intersected = i;
                                    }
                                }
                                // total reflection inside
                            } else {
                                npe = normalize(npe - 2. * cosT * fp_min);

                                if (enter) {
                                    float t;
                                    vec3 fp;
                                    set_a(obj_intersected);
                                    vec4 clr = general_quadrics(obj_intersected, v2, vup, pc_min, pe, npe, true, t, fp);
                                    th_min = t;
                                    fp_min = fp;
                                } else {
                                    th_min = MAX_TH;
                                    for(int i = 0; i < 7; i++) {
                                        set_a(i);
                                        float t;
                                        vec3 fp;
                                        vec4 clr = general_quadrics(i, v2, vup, pc[i], pe, npe, insides[i], t, fp);
                                        if (t < th_min) {
                                            th_min = t;
                                            fp_min = fp;
                                            eta_min = etas[i];
                                            pc_min = pc[i];
                                            color_min = clr;
                                            obj_intersected = i;
                                        }
                                    }
                                    if (th_min == MAX_TH) {
                                        gl_FragColor = color_min;
                                        return;
                                    }
                                }
                            }

                        } else {
                            // hit opaque object
                            if(in_shadow(pe, obj_intersected))
                                color_min = vec4(color_min.rgb * shadow_intensity, 1.);
                            gl_FragColor = color_min;
                            return;
                        }

                    }

                    th_min = MAX_TH;
                    for(int i = 0; i < 7; i++) {
                        if (isReflective(i) || isTransparent(i))
                            continue;
                        set_a(i);
                        float t;
                        vec3 fp;
                        vec4 clr = general_quadrics(i, v2, vup, pc[i], pe, npe, insides[i], t, fp);
                        if (t < th_min) {
                            th_min = t;
                            color_min = clr;
                            obj_intersected = i;
                        }
                    }
                    if (th_min == MAX_TH)
                        gl_FragColor = defaultColor;
                    else {
                        if(in_shadow(pe + th_min * npe, obj_intersected))
                            color_min = vec4(color_min.rgb * shadow_intensity, 1.);
                        gl_FragColor = color_min;
                    }
                } else {
                    if(in_shadow(pe + th_min * npe, obj_intersected))
                        color_min = vec4(color_min.rgb * shadow_intensity, 1.);
                    gl_FragColor = color_min;
                }
            }
        }
    </script>

    <script type="text/javascript" src="lib/jquery-2.1.4.js"></script>
    <script type="text/javascript" src="scripts/webgl-utils.js"></script>
    <script type="text/javascript" src="scripts/initShaders.js"></script>
    <script type="text/javascript" src="scripts/MV.js"></script>


</head>

<body>
<!--nav-->
<nav class="navbar navbar-inverse navbar-fixed-top" role="navigation">
    <ul class="fancyNav">
        <li><a class="active" href=index.html>3D Objects</a></li>
    </ul>
</nav>

<!--content-->
<div class="container-fluid">
    <div class="row row-offcanvas row-offcanvas-left">
        <!--Left Column start-->
        <div class="col-sm-2 sidebar sidebar-left sidebar-offcanvas">

            <ul class="myAccordion" id="accordion_L">
                <!--Basic Images-->
                <li class="panel">
                    <a class="myControlTitle" data-toggle="collapse" data-parent="#accordion_L" href="#basicImages">
                        Texture Images
                    </a>
                    <div id="basicImages" class="inCollapse collapse in">

                        <div class="imgbox_container">
                            <h6><span title="Insert or DragnDrop texture image here">Solid Texture Image</span></h6>
                            <div>
                                <div id="container1" class="imgThumb_container">
                                    <div class="btn-file">
                                        <input type="file" id="inputFileToLoad1">
                                    </div>
                                    <div id="container1image" class="imgThumb"></div>
                                </div>
                            </div>
                        </div>

                        <div class="imgbox_container">
                            <h6><span title="Insert or DragnDrop texture image here">Texture Mapping Image</span></h6>
                            <div>
                                <div id="container2" class="imgThumb_container">
                                    <div class="btn-file">
                                        <input type="file" id="inputFileToLoad2">
                                    </div>
                                    <div id="container2image" class="imgThumb"></div>
                                </div>
                            </div>
                        </div>

                        <div>
                            <h6>Style Control</h6>
                            <div class="slider_box">
                                <input id="styleControl_slider" data-slider-id='customSlider' type="text" class="span2"
                                       value=""/>
                            </div>
                            <div>
                                <div class="h7 inlineBlock">Bright</div>
                                <div class="h7 pull-right">Dark</div>
                            </div>

                            <div class="slider_box">
                                <input id="specSharpness_slider" data-slider-id='customSlider' type="text" class="span2"
                                       value=""/>
                            </div>
                            <div>
                                <div class="h7 inlineBlock">Specular Sharpness</div>
                                <div class="h7 pull-right"></div>
                            </div>
                        </div>
                    </div>
                </li>

            </ul>
        </div>
        <!--Left Column end-->
        <!--Main Canvas start-->
        <div class="col-sm-8 main">
            <!--
            <p class="pull-left visible-xs">
               <button type="button" class="btn btn-primary btn-xs" data-toggle="offcanvas">Toggle nav</button>
            </p>
            -->
            <!--
           <img src="img/solidColor.jpg" class="img-responsive" alt="our canvas">
           -->
            <div class="canvas_container">
                <canvas id="gl-canvas">
                    Oops ... your browser doesn't support the HTML5 canvas element
                </canvas>

                <svg id="lightPosition_container" xmlns="http://www.w3.org/2000/svg">


                </svg>

            </div>

        </div>
        <!--Main Canvas end-->

        <!--Right column start-->
        <div class="col-sm-2 sidebar sidebar-right">
            <ul class="myAccordion" id="accordion_R">
                <!--Light-->
                <li class="panel">
                    <a class="myControlTitle collapsed" data-toggle="collapse" data-parent="#accordion_R"
                       href="#lightControl">
                        Light
                    </a>
                    <div id="lightControl" class="inCollapse collapse">
                        <!--                        <div class="btn_container">-->
                        <!--                            <button class="btn btn-block" id="btn_addLight">ADD LIGHT</button>-->
                        <!--                        </div>-->
                        <div class="checkbox">
                            <label>
                                <span class="h6 inlineBlock"> Area Light </span>
                                <div class="checkbox_container pull-right">
                                    <input type="checkbox" id="checkAreaLightSelect">
                                    <div class="pseudoCheckbox"></div>
                                </div>
                            </label>
                        </div>
                        <div class="checkbox">
                            <label>
                                <span class="h6 inlineBlock"> Directional Light </span>
                                <div class="checkbox_container pull-right">
                                    <input type="checkbox" id="checkDirectionalLightSelect">
                                    <div class="pseudoCheckbox"></div>
                                </div>
                            </label>
                        </div>
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Light Intensity </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="lightIntensity">
                            </div>
                            <div class="slider_box">
                                <input id="lightIntensity_silder" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>
                        <div id="lights_container">
                            <ul class="myAccordion" id="accordion_Lights">

                            </ul>
                        </div>
                    </div>
                </li>

                <li class="panel">
                    <a class="myControlTitle collapsed" data-toggle="collapse" data-parent="#accordion_R"
                       href="#coordinate">
                        Move Light Position (mainly for point light)
                    </a>
                    <div id="coordinate" class="inCollapse collapse">
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> X </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="light_X">
                            </div>
                            <div class="slider_box">
                                <input id="X_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Y </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="light_Y">
                            </div>
                            <div class="slider_box">
                                <input id="Y_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Z </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="light_Z">
                            </div>
                            <div class="slider_box">
                                <input id="Z_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>

                    </div>
                </li>
                <li class="panel">
                    <a class="myControlTitle collapsed" data-toggle="collapse" data-parent="#accordion_R"
                       href="#lightDirection">
                        Move Light Direction (for directional light)
                    </a>
                    <div id="lightDirection" class="inCollapse collapse">
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> X </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="Dir_light_X">
                            </div>
                            <div class="slider_box">
                                <input id="Dir_X_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Y </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="Dir_light_Y">
                            </div>
                            <div class="slider_box">
                                <input id="Dir_Y_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>
                        <div class="slider_container">
                            <span class="h6 inlineBlock"> Z </span>
                            <div class="pull-right">
                                <input type="text" class="form-control mytextbox text-center" id="Dir_light_Z">
                            </div>
                            <div class="slider_box">
                                <input id="Dir_Z_slider" data-slider-id='customSlider' type="text"/>
                            </div>
                        </div>

                    </div>
                </li>
            </ul>
        </div>
        <!--Right column end-->
    </div>
</div>


<!-- light templete -->
<script type="text/template" data-template="light">
    <li class="panel" data-id="{{id}}" id="lightPanel{{id}}">
        <a class="myControlTitle myLightsTitle collapsed" data-toggle="collapse" data-parent="#accordion_Lights"
           href="#light{{id}}">
            <div class="destroy"></div>
            LIGHT{{id}}
        </a>
        <div id="light{{id}}" class="lightBox inCollapse collapse">
            <div>
                <span class="h6 inlineBlock"> Color </span>
                <div class="pull-right">
                    <input class="colorPicker" type="hidden">
                </div>
            </div>
        </div>
    </li>

</script>

<!-- Bootstrap core JavaScript  ================================================== -->
<script src="lib/bootstrap.min.js"></script>
<script src="lib/bootstrap-slider.js"></script>

<!--colorpicker-->
<script src="lib/jquery.minicolors.min.js"></script>

<!-- IE10 viewport hack for Surface/desktop Windows 8 bug -->
<script src="lib/ie10-viewport-bug-workaround.js"></script>

<!-- More -->
<script type="text/javascript" src="scripts/dragndrop.js"></script>
<script type="text/javascript" src="scripts/cube.js"></script>
<script type="text/javascript" src="scripts/sliders.js"></script>
<script type="text/javascript" src="scripts/addLight.js"></script>

</body>
</html>
